apply plugin: 'groovy'

version = '1.0'
description = 'cucumber-jvm parallel runner'

repositories {
    mavenCentral()
}

ext {
    groovyVersion = '2.3.8'
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
        groovy {
            exclude 'org/dustinl/cucumber/cucumberParallelJvm.groovy'
        }
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies {
    compile 'info.cukes:cucumber-groovy:1.2.0'
    compile 'org.apache.commons:commons-io:1.3.2'
    testCompile 'junit:junit:4.11'
    provided "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

test {
    testLogging { exceptionFormat "full" }
}

task processScript(type: Copy) {
    from('src/main/script/')
    from('src/main/groovy/')
    into("${buildDir}/script")
}

task testScript(type:Exec, dependsOn:[processScript,jar])  {
    commandLine "$buildDir/script/cucumberParallelJvm", '--plugin', 'pretty', '--plugin', 'json:target/cucumber.json',
    '--glue',  'classpath:org.dustinl.cucumber.test', '--thread', '4', '-d', jar.archivePath
}


clean {
    delete 'target'
}

build.dependsOn testScript
